[object_type]
functions="def spnTmpDbg (self, crd) [
			spawn('walker-2.dbg', crd[0], crd[1], self.facing, [die()] )
		];
		
		def spnDbg (self, crd) [
			spawn('walker-2.dbg', crd[0], crd[1], self.facing)
		];
		
		def getAxels (self) 
			map(range(size(axels)), 'axel_num', 
				#At this point, 'axel' is an x,y pair.#
				map(range(2), 'n', rot_axel[n]+midpoint[n])
				where rot_axel = orbit_obj(0, 0,
					 						angle(0, 0, axel[0], axel[1]) + rotate,
											length(0, 0, axel[0], axel[1]))
				where axel = axels[axel_num])
			where axels = self.consts.axels
			where midpoint = [self.midpoint_x, self.midpoint_y]
			where rotate = self.rotate;
			
		def getUpperLegRot (self)
			map(range(size(axels)), 'axel_num', 
				map(range(size(axels[axel_num])), 'crd_num', 
					#Here, leg is the upper leg object.#
					leg.vars.bjRot+rotate
					where leg = legs[axel_num][crd_num]
				)
			)
			where axels = getAxels(self)
			where legs = self.vars.legs
			where rotate = self.rotate;
			
		def getUpperLegLoc (self)
			map(range(size(axels)), 'axel_num', 
				map(range(size(axels[axel_num])), 'crd_num', 
					#Here, leg is the upper leg object.#
					orbit_obj(
						axels[axel_num][0],
						axels[axel_num][1],
						90 + angle[axel_num][crd_num] + legs.vars.bjRot,
						length(0,0,leg.consts.bjLoc[0],leg.consts.bjLoc[1])
					)
					where leg = legs[axel_num][crd_num]
				)
			)
			where axels = getAxels(self)
			where legs = self.vars.legs
			where angle = getUpperLegRot(self);
			
		def getKneeLoc (self)
			map(range(size(axels)), 'axel_num', 
				map(range(size(axels[axel_num])), 'crd_num',
					orbit_obj(
						axels[axel_num][0],
						axels[axel_num][1],
						90 + angle[axel_num][crd_num] + legs.vars.bjRot,
						length(leg.consts.ljLoc[0],leg.consts.ljLoc[1],leg.consts.bjLoc[0],leg.consts.bjLoc[1])
					)
					where leg = legs[axel_num][crd_num]
				)
			)
			where axels = getAxels(self)
			where legs = self.vars.legs
			where angle = getUpperLegRot(self);
			
		def setUpperLegs (self, locs, rots)
			map(range(size(locs)), 'varA',
				map(range(size(locs[varA])), 'varB',
					[
						set(legcp.midpoint_x, locs[varA][varB][0]),
						set(legcp.midpoint_y, locs[varA][varB][1]),
						set(legcp.rotate, rotate),
					] where legcp = legs[varA][varB]
					where rotate = rots[varA][varB]
				)
			)
			where legs = self.vars.legs;
			
		def getLowerLegLoc (self)
			map(range(size(knees)), 'knee_num', 
				map(range(size(knees[knee_num])), 'knee_sub',
					orbit_obj(
					knee[0],
					knee[1],
					90 + pRot + leg.vars.ljRot,
					length(0,0,leg.vars.leg.consts.joint[0],leg.vars.leg.consts.joint[1])
					)
					where knee = knees[knee_num][knee_sub] #coord#
					where leg = legs[knee_num][knee_sub] #object#
					where pRot = gulr[knee_num][knee_sub]
				)
			)
			where legs = self.vars.legs
			where knees = getKneeLoc(self)
			where gulr = getUpperLegRot(self);
			
		def getLowerLegRot (self)
			map(range(size(axels)), 'axel_num', 
				map(range(size(axels[axel_num])), 'crd_num', 
					#Here, leg is the upper leg object.#
					leg.vars.bjRot+leg.vars.ljRot+rotate
					where leg = legs[axel_num][crd_num]
				)
			)
			where axels = getAxels(self)
			where legs = self.vars.legs
			where rotate = self.rotate;
			
		def setLowerLegs (self, locs, rots)
			map(range(size(locs)), 'varA',
				map(range(size(locs[varA])), 'varB',
					[
						set(legcp.midpoint_x, locs[varA][varB][0]),
						set(legcp.midpoint_y, locs[varA][varB][1]),
						set(legcp.rotate, rotate),
					] where legcp = legs[varA][varB].vars.leg
					where rotate = rots[varA][varB].vars.leg
				)
			)
			where legs = self.vars.legs;
			
		def getFeetLoc (self)
			map(range(size(knees)), 'knee_num', 
				map(range(size(knees[knee_num])), 'knee_sub',
					orbit_obj(
					knee[0],
					knee[1],
					90 + pRot + leg.vars.ljRot,
					length(0,0,leg.vars.leg.consts.joint[0]-leg.vars.leg.consts.foot[0],
							leg.vars.leg.consts.joint[1]-leg.vars.leg.consts.foot[1])
					)
					where knee = knees[knee_num][knee_sub] #coord#
					where leg = legs[knee_num][knee_sub] #object#
					where pRot = gulr[knee_num][knee_sub]
				)
			)
			where legs = self.vars.legs
			where knees = getKneeLoc(self)
			where gulr = getUpperLegRot(self);
			
		def getFeetTouch (self, level)
			map(range(size(feet)), 'axel',
				map(range(size(feet[axel])), 'foot',
					solid(level, feet[axel][foot][0], feet[axel][foot][1], 2, 2)
				)
			)
			where feet = getFeetLoc(self);
			
		def newStepPos (self, level)
			map(range(size(feet)), 'axel',
				map(range(size(feet[axel])), 'foot',
					if(fs = 0,
						if(sl != [-1,-1], [-1,-1], sl),
						if(sl = [-1,-1], fl, sl)
					)
					where fl = feet[axel][foot]
					where fs = tState[axel][foot]
					where sl = stepLoc[axel][foot]
				)
			)
			where feet = getFeetLoc(self)
			where tState = getFeetTouch(self, level)
			where stepLoc = self.vars.stepLoc;
		
		def getRealFootLoc (self, level)
			map(range(size(prints)), 'axel',
				map(range(size(prints[axel])), 'foot',
					if(prints[axel][foot] = [-1, -1], 
						rotFeet[axel][foot], 
						prints[axel][foot]
					)
				)
			)
			where rotFeet = getFeetLoc(self)
			where prints = newStepPos(self, level)
		;
			
		def getFootBodyDist (self, level) #side b#
			map(range(size(axels)), 'axel',
				map(range(size(axels[axel])), 'leg',
					length(axels[axel][0], axels[axel][1], feet[axel][leg][0], feet[axel][leg][1])
				)
			)
			where axels = getAxels(self)
			where feet = getRealFootLoc(self, level)
		;
		
		def getUpperLegLen (self) #side a#
			abs(self.vars.legs[0][0].consts.bjLoc[1])+abs(self.vars.legs[0][0].consts.ljLoc[1])
		;
		
		def getLowerLegLen (self) #side c#
			abs(self.vars.legs[0][0].vars.leg.consts.joint[1])+abs(self.vars.legs[0][0].vars.leg.consts.foot[1])
		;
			
		def getLLRealRot (self, level)
			map(range(size(bs)), 'axel',
				map(range(size(bs[axel])), 'leg',
					-angle_alpha(a,b,c)		#needs proper bend direction checking.#
					where b = bs[axel][leg]
				)
			)
			where a = getUpperLegLen(self)
			where c = getLowerLegLen(self)
			where bs = getFootBodyDist(self, level)
		;
			
		def getLLRealLoc (self, level)
			map(range(size(rots)), 'axel',
				map(range(size(rots)), 'leg',
					orbit_obj(fprt[0], fprt[1], rot, len)
					where rot = rots[axel][leg]-90
					where len = length(legs[axel][leg].vars.leg.consts.foot[0], legs[axel][leg].vars.leg.consts.foot[1], 0, 0)
					where fprt = ftLoc[axel][leg]
				)
			)
			where legs = self.vars.legs
			where rots = getLLRealRot(self, level)
			where ftLoc = getRealFootLoc(self, level)
		;
			
		def getRealKnee (self, level)
			map(range(size(rots)), 'axel',
				map(range(size(rots)), 'leg',
					orbit_obj(fprt[0], fprt[1], rot, len)
					where rot = rots[axel][leg]-90
					where fprt = ftLoc[axel][leg]
				)
			)
			where len = getLowerLegLen(self)
			where legs = self.vars.legs
			where rots = getLLRealRot(self, level)
			where ftLoc = getRealFootLoc(self, level)
		;
			
#broken	def getULRealRot (self, level)
			map(range(size(knees)), 'axel',
				map(range(size(knees)), 'leg',
					angle(0, 0, knees[axel][leg][0]-axels[axel][0], knees[axel][leg][1]-axels[axel][1])
				)
			)
			where axels = getAxels(self)
			where knees = getRealKnee(self, level)
		;
#		
		def getULRealRot (self, level)
			map(range(size(bs)), 'axel',
				map(range(size(bs[axel])), 'leg',
					angle_theta(a,b,c)		#needs proper bend direction checking.#
					where b = bs[axel][leg]
				)
			)
			where a = getUpperLegLen(self)
			where c = getLowerLegLen(self)
			where bs = getFootBodyDist(self, level)
		;
		
		def getULRealLoc (self, level)
			map(range(size(rots)), 'axel',
				map(range(size(rots)), 'leg',
					orbit_obj(axels[axel][0], axels[axel][1], 
						rots[axel][leg]+90,
						length(legs[axel][leg].consts.bjLoc[0],
							legs[axel][leg].consts.bjLoc[1], 0, 0))
				)
			)
			where axels = getAxels(self)
			where rots = getULRealRot(self, level)
			where legs = self.vars.legs
		;"
id="walker-2"
is_human="true"
on_create="[
		map(range(size(self.consts.axels)), 'avar', 
			map(range(2), 'bvar', 
				spawn('walker-2.upper-leg', x, y, self.facing, set_var('listAddress', [avar,bvar])))),
		set(vars.legs, map(range(size(consts.axels)), ['-','+'])),
		set(vars.stepLoc, map(range(size(consts.axels)), [[-1,-1], [-1,-1]])),
		set(vars.axState, map(range(size(consts.axels)), ['ok','ok'])), #ok, nopres, lgone, ugone#
	]"
on_ctrl_left="map(range(size(knees)), 'axel',
				map(range(size(knees)), 'leg',
					[spawn('walker-2.dbg', axels[axel][0], axels[axel][1], self.facing),
					spawn('walker-2.dbg', knees[axel][leg][0], knees[axel][leg][1], self.facing),
					#debug(angle(axels[axel][0], axels[axel][1], knees[axel][leg][0], knees[axel][leg][1])),#
					#OK, what the heck is going on here - angle is returning the wrong data, and it bloody well shouldn't be.#
					debug(axels[axel][0], axels[axel][1], knees[axel][leg][0], knees[axel][leg][1])
					]
				)
			)
			where axels = getAxels(self)
			where knees = getRealKnee(self, level)"
on_ctrl_right="[
		map(getRealKnee(self, level), 'crd', spnDbg(self, crd[0])),
		debug(getULRealRot(self, level)),
	]"
on_done_create="set(self.vars.legs,
			map(range(size(self.consts.axels)), 'index',
				map(range(size(self.consts.axels[index])), 'subIndex', 
					self.tmp.legs[context.index*2+subIndex])))"
on_end_anim="animation('normal')"
on_process="[
		#debug(getFootBodyDist(self, level)),#
		set(self.vars.stepLoc, nsp),
		setUpperLegs(self, recUpLegPos, getULRealRot(self, level)),
		setLowerLegs(self, recLowLegPos, getLLRealRot(self, level)),
		if(ctrl_down, set(y, y+2)),
		if(ctrl_up, set(y, y-2)),
	]
	where recUpLegPos = getULRealLoc(self, level)
	where recLowLegPos = getLLRealLoc(self, level)
	where nsp = newStepPos(self, level)"
	[editor_info]
	category="crazy-stuff"
	help="A small car-sized robot, used to scale cliffs. Warning: Do not occupy."
	[/editor_info]

	[consts]
	axels="[[-28,4], [56,4]]"
	[/consts]

	[vars]
	axRot="[]"
	legs="[[]]"
	overshoot="0"
	rotvel="0"
	[/vars]

	[animation]
	duration="1000"
	frame_info="0,0,0,0,447,610,98,44"
	frames="1"
	id="normal"
	image="compiled-1.png"
	item_area="all"
	rect="447,610,544,653"
	[/animation]

	[object_type]
	id="upper-leg"
	on_spawned="[
			#debug('Spawning leg ' + str(self.vars.listAddress)),#
			set(spawner.tmp.legs, spawner.tmp.legs + [self]),
			if(self.vars.listAddress[1] = 0, set(z, z-5), set(z, z+5)),
			spawn('walker-2.lower-leg', x, y, facing),
		]"
		[vars]
		bjRot="45"
		ljRot="-65"
		[/vars]

		[consts]
		bjLoc="[-2,28]"
		ljLoc="[-2,-36]"
		[/consts]

		[animation]
		duration="1000"
		frames="1"
		id="normal"
		image="experimental/spiderbot.png"
		item_area="all"
		pad="0"
		rect="10,64,21,105"
		[/animation]

	[/object_type]

	[object_type]
	id="lower-leg"
	on_end_anim="animation('normal')"
	on_spawned="[set(spawner.vars.leg, child),
		set(z, spawner.z + if(spawner.z<0, 1, -1))]"
		[consts]
		foot="[0,60]"
		joint="[-2,-54]"
		[/consts]

		[animation]
		duration="1000"
		frames="1"
		id="normal"
		image="experimental/spiderbot.png"
		item_area="all"
		pad="0"
		rect="34,64,41,122"
		[/animation]

	[/object_type]

	[object_type]
	id="dbg"
	on_create="set(z, 150)"
	on_end_anim="animation('normal')"
	z="150"
		[animation]
		duration="5"
		frames="2"
		id="normal"
		image="experimental/spiderbot.png"
		item_area="all"
		pad="0"
		rect="87,65,94,72"
		[/animation]

	[/object_type]

[/object_type]

