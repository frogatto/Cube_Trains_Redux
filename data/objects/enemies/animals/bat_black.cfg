	[object_type]
	id=bat_black
	activation_border=1000

	[vars]
		points_value="14"
	[/vars]
	[consts]
		basic_type="'bat_black'"
		thrown_type="'bat_black.thrown'"
		basic_animation="'fly'"
	[/consts]
	prototype=ant_flying

	solid_dimensions=none

	[properties]
		should_dive="if(animation = 'cling' and
						cycle - vars.last_clung > 100 and	
						level.player.y > y + 50 and level.player.y < y + 900 and
						level.player.midpoint_x > midpoint_x - 250 and
						level.player.midpoint_x < midpoint_x + 250, 1, 0)"
		should_climb="if(abs(midpoint_y - vars.init_y) > 300 or abs(midpoint_x - vars.init_x) > 300, 1, 0)"

		should_cling="if(abs(midpoint_x - vars.init_x) < 10 and abs(midpoint_y - vars.init_y) < 10, 1, 0)"

		begin_dive="def() [		animation('wake'),
								if(level.player.midpoint_x > midpoint_x, facing(1), facing(-1)),
								set(vars.behavior, 'dive'),
								set(vars.target_x, level.player.midpoint_x),
								set(vars.target_y, level.player.y),
								add(velocity_y,200)]"
		begin_climb="def() [	set(vars.behavior, 'climb')]"
		begin_cling="def() [	animation('cling'),
								set(vars.last_clung, cycle),
								set(vars.behavior, ''),
								set(velocity_x,0),
								set(velocity_y,0)]"
		
		process_dive="def() [	if(level.player.midpoint_y > midpoint_y, add(velocity_y, 8),add(velocity_y,-8) ),
								if(level.player.midpoint_x > midpoint_x, [try_facing(1),add(velocity_x, 12)],[try_facing(-1),add(velocity_x,-12)] ),
							   	if(should_climb, begin_climb() )]"
							   	
		process_climb="def() [	if(midpoint_x < init_x, [try_facing(1),add(velocity_x, 5)],[try_facing(-1),add(velocity_x,-5)] ),
								if(midpoint_y < init_y, add(velocity_y, 8),add(velocity_y,-8) ),
								if(should_cling, begin_cling())
									]"
					    				
		
		try_facing="def(direction) if(direction != facing,fire_event('turn'))"
	[/properties]

		on_create="[	set(vars.behavior,''),
						animation('cling'),
						set(vars.init_y, midpoint_y),
						set(vars.init_x, midpoint_x)
					]"

		on_end_anim="if(animation in ['cling'], animation('cling'),animation('fly'))"

		#on_process="debug(animation + vars.behavior),"

		##Note:  the proto will set our y velocity for regular soaring.  We only override this if we're busy diving or climbing.
		on_process_cling="if(should_dive, begin_dive())"
		on_process_fly="
				switch(vars.behavior,
							'dive', process_dive(),
							'climb', process_climb(),
							'regular', [%PROTO% + (wave(cycle*5+1000)/50) , if(should_dive, begin_dive())])"
		
		on_process_turn="fire_event('process_fly')"

		on_lose_wings="#don't lose wings#"

		[base:animation]
		image=enemies/bat-black.png
		pad=3
		body_area=all
		accel_y=0
		[/animation]

		[animation]
		id=fly
		rect=1,32,26,70
		frames=4
		duration=4
		accel_x=0
		[/animation]

		[animation]
		id=cling
		rect=1,1,18,28
		frames=2
		duration=16
		accel_x=0
		[/animation]

		[animation]
		id=wake
		rect=43,1,60,28
		frames=1
		duration=6
		accel_x=0
		[/animation]

		[animation]
		id=spring
		rect=1,32,26,70
		frames=1
		duration=3
		[/animation]

		[animation]
		id=turn
		rect=1,74,26,112
		frames=2
		duration=5
		[/animation]

		[animation]
		id=hurt
		rect=120,32,145,70
		[/animation]

		
		
		[object_type]	
		id=thrown
		prototype=throwable_small_enemy
		solid_dimensions=enemy,common

		[consts]
			basic_type="'bat_black'"
			thrown_type="'bat_black.thrown'"
			basic_animation="'fly'"
		[/consts]

		on_spat="[%PROTO%, set(vars.bomb, null()), set(attached_objects, [])]"

			[base:animation]
			image=enemies/bat-black.png
			[/animation]

			[animation]
			id=thrown
			thrown_area=all
			duration=60
			rect=149,32,174,70
			frames=1
			body_area=none
			[/animation]
	
			[animation]
			id=on_back
			rect=149,32,174,70
			frames=2
			duration=5
			body_area=all
			[/animation]
		[/object_type]
	[/object_type]
